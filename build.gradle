buildscript {
    ext {
        pluginName = 'analysis-morfologik'
        pluginClassname = 'pl.allegro.tech.elasticsearch.plugin.analysis.morfologik.AnalysisMorfologikPlugin'
        pluginDescription = 'Morfologik Polish Lemmatizer plugin for Elasticsearch'
        versions = [
                'elasticsearch'         : '5.4.0',
                'lucene'                : '6.5.0',
                'log4j'                 : '2.6.2',
                'embedded_elasticsearch': '2.1.0',
                'spock'                 : '1.0-groovy-2.4'
        ]
    }
}

plugins {
    id 'com.bmuschko.nexus' version '2.3.1'
    id "io.codearte.nexus-staging" version "0.8.0"
}

apply plugin: 'java'
apply plugin: 'groovy'

group = 'pl.allegro.tech.elasticsearch.plugin'
version = versions.elasticsearch

repositories {
    mavenCentral()
}

configurations {
    providedCompile
    wagon
    releaseJars {
        extendsFrom runtime
        exclude group: 'org.elasticsearch'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'org.slf4j'
        exclude group: 'log4j'
        exclude module: 'lucene-core'
        exclude module: 'lucene-analyzers-common'
    }
}

dependencies {
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: versions.elasticsearch
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-morfologik', version: versions.lucene
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: versions.log4j
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: versions.log4j
    testCompile group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testCompile group: 'pl.allegro.tech', name: 'embedded-elasticsearch', version: versions.embedded_elasticsearch
    testCompile group: 'org.elasticsearch.client', name: 'transport', version: versions.elasticsearch

}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

test {
    systemProperties['path.home'] = System.getProperty("user.dir")
    systemProperties['elasticsearchVersion'] = versions.elasticsearch
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
}

task makePluginDescriptor(type: Copy) {
    from 'src/main/templates'
    into 'build/tmp/plugin'
    expand([
            'descriptor': [
                    'name'                : pluginName,
                    'classname'           : pluginClassname,
                    'description'         : pluginDescription,
                    'jvm'                 : true,
                    'site'                : false,
                    'isolated'            : true,
                    'version'             : project.property('version'),
                    'javaVersion'         : project.property('targetCompatibility'),
                    'elasticsearchVersion': versions.elasticsearch
            ]
    ])
}

task buildPluginZip(type: Zip, dependsOn: [':jar', ':makePluginDescriptor']) {
    from files(libsDir)
    from configurations.releaseJars
    from 'build/tmp/plugin'
    into 'elasticsearch'
    classifier = 'plugin'
}

task unpackPlugin(type: Copy, dependsOn: [':buildPluginZip']) {
    delete "plugins"
    from files(libsDir)
    from configurations.releaseJars
    from 'build/tmp/plugin'
    into "plugins/${pluginName}"
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
        events = ["failed", "skipped", "passed"]
        showCauses = true
        showStackTraces = true
    }
}

clean {
    delete "plugins"
}

artifacts {
    archives buildPluginZip
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

nexusStaging {
    packageGroup = 'pl.allegro'
    numberOfRetries = 15
    delayBetweenRetriesInMillis = 5000
}

modifyPom {
    project {
        name 'elasticsearch-analysis-morfologik'
        description 'Morfologik Polish Lemmatizer plugin for Elasticsearch'
        url 'https://github.com/allegro/elasticsearch-analysis-morfologik'
        inceptionYear '2017'

        scm {
            url 'https://github.com/allegro/elasticsearch-analysis-morfologik'
            connection 'scm:git@github.com:allegro/elasticsearch-analysis-morfologik.git'
            developerConnection 'scm:git@github.com:allegro/elasticsearch-analysis-morfologik.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'yukon'
                name 'YUKON'
            }
        }
    }
}